// IPA transcription
transcription: syllables
  | group
  | segments
  | phonetic
  | phonemic

// bracketed types
phonetic: "[" (syllables | group | segments) "]"
phonemic: "/" (syllables | group | segments) "/"

// segmental sequences
group: major+ | minor+
major: minor+ "‖"
minor: syllables+ "|" | syllables+
syllables: [TONESTEP] [STRESS] syllable ([TONESTEP] [suprasegmental] syllable)*
segments: [STRESS] segment+ (suprasegmental segment+)*

// syllables

syllable: onset rime RIGHTTONECONTOUR | LEFTTONECONTOUR onset rime | onset tonalrime | onset rime

tonalrime: tonalnucleus coda
tonalnucleus: (vowel* (tonalvowel | syllabic TONEMARK [LENGTH])+ vowel*)+

rime: nucleus coda
nucleus: (vowel+ | (syllabic [LENGTH])+)+
coda: consonant*
onset: consonant*

// segmentals
segment: (consonant | vowel | syllabic [TONEMARK] [LENGTH] | tonalvowel)+

tonalvowel: tonemarkedv | tonaldipthong
tonaldipthong: tonemarkedv DOUBLEBREVE tonemarkedv
  | tonemarkedv DOUBLEBREVE V [vfeatures] [LENGTH]
  | V [vfeatures] [LENGTH] DOUBLEBREVE tonemarkedv
tonemarkedv: V [vfeatures] TONEMARK [vfeatures] [LENGTH]
vowel: V [vfeatures] [LENGTH] | dipthong

consonant: C [cfeatures] | affricate
dipthong: V [vfeatures] [LENGTH] DOUBLEBREVE V [vfeatures] [LENGTH]
affricate: C [cfeatures] DOUBLEBREVE C [cfeatures]

DOUBLEBREVE: ("\u035c" | "\u0361")

// suprasegmentals
suprasegmental: STRESS | BREAK | LINK
STRESS: "ˈ" | "ˌ"
BREAK: "."
LINK: "‿"

// length modifiers
LENGTH: "ː" | "ˑ" | "\u0306"

// consonant features
// the explicit permutations of (" ̩" | " ̍") with other features is annoying
// but, it's intended to separate syllabic consonants from nonsyllabic ones
syllabic: C SYLLABICMARK NONSYLLABIC* vfeatures*
  | C NONSYLLABIC* SYLLABICMARK vfeatures*
  | C NONSYLLABIC* vfeatures* SYLLABICMARK
  | C SYLLABICMARK vfeatures* NONSYLLABIC*
  | C vfeatures* SYLLABICMARK NONSYLLABIC*
  | C vfeatures* NONSYLLABIC*  SYLLABICMARK
cfeatures: (cfeature | XFEATURE)+
cfeature: NONSYLLABIC | SYLLABICMARK
SYLLABICMARK: "\u0329" | "\u030D"
NONSYLLABIC: "ʰ"
  | "ʲ"
  | "ʷ"
  | "ˠ"
  | "ˡ"
  | "ˢ"
  | "ˤ"
  | "\u031a"
  | "\u0325"
  | "\u0329"
  | "\u032a"
  | "\u032c"
  | "\u0334"
  | "\u033a"
  | "\u033b"
  | "\u033c"
  | "\u207f"
  | XFEATURE

// vowel features
vfeatures: (VFEATURE | XFEATURE)+
VFEATURE: "˞"
  | "\u02d4"
  | "\u02d5"
  | "\u0303"
  | "\u0308"
  | "\u0318"
  | "\u0319"
  | "\u031c"
  | "\u031d"
  | "\u031e"
  | "\u031f"
  | "\u0320"
  | "\u032f"
  | "\u0339"
  | "\u033d"

// tones
TONEMARK: "\u0300"
  | "\u0301"
  | "\u0302"
  | "\u030b"
  | "\u030c"
  | "\u030f"
  | "\u1dc4"
  | "\u0304"
TONESTEP: "↓" | "↑"
LEFTTONECONTOUR: ("꜒" | "꜓" | "꜔" | "꜕" | "꜖")+
RIGHTTONECONTOUR: ("˥" | "˦" | "˧" | "˨" | "˩")+ | "↗" | "↘"

XFEATURE: "\u0324" | "\u0330"

// consonants
C: "!"
  | "b"
  | "c"
  | "ç"
  | "d"
  | "f"
  | "h"
  | "j"
  | "k"
  | "l"
  | "m"
  | "n"
  | "p"
  | "q"
  | "r"
  | "s"
  | "t"
  | "v"
  | "w"
  | "x"
  | "z"
  | "ð"
  | "ħ"
  | "ŋ"
  | "ǀ"
  | "ǁ"
  | "ǂ"
  | "ɓ"
  | "ɕ"
  | "ɖ"
  | "ɗ"
  | "ɟ"
  | "ɠ"
  | "ɡ"
  | "ɢ"
  | "ɣ"
  | "ɥ"
  | "ɦ"
  | "ɧ"
  | "ɬ"
  | "ɭ"
  | "ɮ"
  | "ɰ"
  | "ɱ"
  | "ɲ"
  | "ɳ"
  | "ɴ"
  | "ɸ"
  | "ɹ"
  | "ɺ"
  | "ɻ"
  | "ɽ"
  | "ɾ"
  | "ʀ"
  | "ʁ"
  | "ʂ"
  | "ʃ"
  | "ʄ"
  | "ʈ"
  | "ʋ"
  | "ʍ"
  | "ʎ"
  | "ʐ"
  | "ʑ"
  | "ʒ"
  | "ʔ"
  | "ʕ"
  | "ʘ"
  | "ʙ"
  | "ʛ"
  | "ʜ"
  | "ʝ"
  | "ʟ"
  | "ʡ"
  | "ʢ"
  | "ʼ"
  | "ˀ"
  | "β"
  | "θ"
  | "χ"
  | "ⱱ"

// vowels
V: "a"
  | "æ"
  | "e"
  | "i"
  | "o"
  | "ø"
  | "u"
  | "y"
  | "œ"
  | "ɐ"
  | "ɑ"
  | "ɒ"
  | "ɔ"
  | "ɘ"
  | "ə"
  | "ɛ"
  | "ɜ"
  | "ɞ"
  | "ɤ"
  | "ɨ"
  | "ɪ"
  | "ɯ"
  | "ɵ"
  | "ɶ"
  | "ʉ"
  | "ʊ"
  | "ʌ"
  | "ʏ"

// Disregard spaces in text
%import common.WS
%ignore WS
